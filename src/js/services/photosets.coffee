'use strict'

angular.module 'flickrSimpleReorder'
.provider 'Photosets', ->
  config = this

  @url = 'https://api.flickr.com/services/rest/'
  @format = 'json'
  @methods =
    getList: 'flickr.photosets.getList'
    getPhotos: 'flickr.photosets.getPhotos'
    reorderPhotos: 'flickr.photosets.reorderPhotos'
  @photosPerRequest = 500

  @$get = [
    '$http'
    '$q'
    'Auth'
    (
      $http
      $q
      Auth
    ) =>
      getList: (userId) ->
        deferred = $q.defer()
        $http.get Auth.signUrl config.url,
          method: config.methods.getList
          primary_photo_extras: 'url_q'
          user_id: userId
        .then (res) ->
          if res.data.stat isnt 'ok'
            deferred.reject res.data.message
          else
            deferred.resolve res.data.photosets
        deferred.promise

      getOnePageOfPhotos: (photosetId, page = 1) ->
        deferred = $q.defer()
        $http.get Auth.signUrl config.url,
          method: config.methods.getPhotos
          photoset_id: photosetId
          page: page
          per_page: config.photosPerRequest
          extras: 'date_upload,date_taken,views'
        .then (res) ->
          if res.data.stat isnt 'ok'
            deferred.reject res.data.message
          else
            deferred.resolve res.data.photoset
        deferred.promise

      getPhotos: (photosetId, total) ->
        deferred = $q.defer()
        totalPages = Math.ceil total / config.photosPerRequest
        photos = []
        requests = _.times totalPages, (page) =>
          # The index generated by Lodash starts from 0,
          # while Flickr requests index starts from 1
          @getOnePageOfPhotos photosetId, (page + 1)
          .then (data) =>
            photos[page] = data.photo
        $q.all(requests).then ->
          deferred.resolve _.flatten(photos)
        deferred.promise

      reorderPhotos: (photosetId, photoIds) ->
        deferred = $q.defer()
        url = Auth.signUrl config.url,
          method: config.methods.reorderPhotos
          photoset_id: photosetId
          photo_ids: photoIds.toString()
        $http.post url
        .then (data) ->
          deferred.resolve data
        , (error) ->
          deferred.reject error
        deferred.promise
  ]

  return
